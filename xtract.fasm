; xtract
; Copyright (C) 2016  LunarFlint

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

format PE GUI 4.0

entry _entrypoint

include 'win32wx.inc'

struct GdiplusStartupInput
	GdiplusVersion			dd ?
	DebugEventCallback		dd ?
	SuppressBackgroundThread	dd ?
	SuppressExternalCodecs		dd ?
ends

struct ImageCodecInfo
	Clsid				db 16 dup ?
	FormatID			db 16 dup ?
	CodecName			dd ?
	DllName				dd ?
	FormatDescription		dd ?
	FilenameExtension		dd ?
	MimeType			dd ?
	Flags				dd ?
	Version				dd ?
	SigCount			dd ?
	SizeSize			dd ?
	SigPattern			dd ?
	SigMask				dd ?
ends


section '.data' data readable writeable

	dateFormat	TCHAR		'yyyy-MM-dd', 0
	.wordLength 	= 		($ - dateFormat) shr 1
	timeFormat	TCHAR		'HH-mm-ss', 0
	.wordLength 	= 		($ - timeFormat) shr 1

	outPath		du		'%HOMEDRIVE%%HOMEPATH%\xtract'
	.pathEnd	du		'\'
	pngFileName	du		'screenshot-'
	.datePtr 	du 		(dateFormat.wordLength - 1) dup ?
	.datePtrEnd	du		?
	.timePtr	du		(timeFormat.wordLength - 1) dup ?
	.timePtrEnd	du		?
	.ext		du		'png', 0

	infoMsgCap	TCHAR		'Info', 0
	outputDirMsgCap	TCHAR		'Output directory created', 0

	infoMsg		TCHAR		'xtract ver 1.0.0', 0Ah, 0Dh, 'By LunarFlint 2016', 0Ah, 0Dh, 0Ah, 0Dh

	outputDirMsg	du		'Output directory at:', 0Ah, 0Dh
	resolvedPath	du		256 dup ?
	.wordLength	=		($ - resolvedPath) shr 1

	pngMime 	du 		'image/png', 0
	.wordLength 	= 		($ - pngMime) shr 1

	pngEncoderId	db 16 dup	?

	mainWcName	TCHAR 		'XTRACT_MAIN_WIN_CLASS', 0
	mainWc		WNDCLASSEX 	sizeof.WNDCLASSEX, CS_HREDRAW + CS_VREDRAW, MainWndProc, 0, 0, NULL, NULL, NULL, NULL, NULL, mainWcName, NULL

	toolWcName	TCHAR 		'XTRACT_TOOL_WIN_CLASS', 0
	toolWc		WNDCLASSEX 	sizeof.WNDCLASSEX, 0, ToolWndProc, 0, 0, NULL, NULL, NULL, NULL, NULL, toolWcName, NULL

	buttonWcName	TCHAR 		'BUTTON', 0

	wTitle		TCHAR 		'xtract', 0
	errorTxt 	TCHAR 		'error', 0

	btnC		TCHAR		'c', 0
	btnS		TCHAR		's', 0
	btnI		TCHAR		'i', 0
	btnX		TCHAR		'x', 0

	msg		MSG
	ps		PAINTSTRUCT

	mainWin		dd		?
	toolWin		dd		?

	bgPen		dd		?
	bgBrush		dd		?



section '.code' code readable executable

	_entrypoint:
		stdcall Main
		invoke ExitProcess, 0

	proc Main
	_Main:
		locals
			gdipToken	dd			?
			gdipInput	GdiplusStartupInput	1, 0, 0, 0
			numEncoder	dd			0
			encoderSz	dd			0
		endl

		;create out folder if not exist
		mov [outPath.pathEnd], 0
		invoke ExpandEnvironmentStrings, outPath, resolvedPath, resolvedPath.wordLength
		mov [outPath.pathEnd], '\'
		invoke CreateDirectory, resolvedPath, NULL
		or eax, eax
		jz @f
			invoke MessageBox, NULL, outputDirMsg, outputDirMsgCap, MB_ICONINFORMATION + MB_OK
		@@:

		;gdi plus
		invoke GdiplusStartup, addr gdipToken, addr gdipInput, NULL
		or eax, eax
		jnz .err

		;query for gdi plus png encoder
		invoke GdipGetImageEncodersSize, addr numEncoder, addr encoderSz
		or eax, eax
		jnz .err

		mov eax, [numEncoder]
		or eax, eax
		jz .err

		push ebx esi edi

		invoke VirtualAlloc, 0, [encoderSz], MEM_COMMIT, PAGE_READWRITE
		mov ebx, eax
		or eax, eax
		jz .err

		invoke GdipGetImageEncoders, [numEncoder], [encoderSz], ebx
		or eax, eax
		jnz .err
		
		mov eax, ebx
		@@:	;loop
		mov esi, [eax + ImageCodecInfo.MimeType] 	; load 2 strs for comparing
		mov edi, pngMime
		mov ecx, pngMime.wordLength 			; word count
		repe cmpsw					; repeat ecx times, compare words
		je @f
		add eax, sizeof.ImageCodecInfo			; move to next structure in the array
		dec [numEncoder]
		jnz @b
		; not found
		invoke VirtualFree, ebx, 0, MEM_RELEASE
		jmp .err

		@@:	; found
		lea esi, [eax + ImageCodecInfo.Clsid]		; copy the id
		mov edi, pngEncoderId
		mov ecx, 4
		rep movsd
		invoke VirtualFree, ebx, 0, MEM_RELEASE

		pop edi esi ebx


		;gdi pens and brushes
		invoke CreatePen, PS_DASH, 1, 12345
		mov [bgPen], eax
		or eax, eax
		jz .err

		invoke CreateSolidBrush, 0
		mov [bgBrush], eax
		or eax, eax
		jz .err

		;win classes
		invoke GetModuleHandle, 0
		mov [mainWc.hInstance], eax
		mov [toolWc.hInstance], eax

		invoke RegisterClassEx, mainWc
		or eax, eax
		jz .err

		invoke RegisterClassEx, toolWc
		or eax, eax
		jz .err

		;create windows
		invoke CreateWindowEx, WS_EX_TOPMOST + WS_EX_LAYERED, mainWcName, wTitle, WS_POPUP, 300, 300, 300, 300, NULL, NULL, [mainWc.hInstance], NULL
		mov [mainWin], eax
		or eax, eax
		jz .err

		invoke SetLayeredWindowAttributes, [mainWin], 0, 127, LWA_ALPHA

		invoke ShowWindow, [mainWin], SW_SHOWNORMAL
		invoke UpdateWindow, [mainWin]

		@@:
			invoke GetMessage, msg, NULL, 0, 0
			or eax, eax
			jz .quit
			
			invoke TranslateMessage, msg
			invoke DispatchMessage, msg
			jmp @b


		.err:
			invoke MessageBox, NULL, errorTxt, NULL, MB_ICONERROR + MB_OK

		.quit:
			invoke DeleteObject, [bgPen]
			invoke DeleteObject, [bgBrush]
			invoke GdiplusShutdown, [gdipToken]
			ret
	endp

	
	proc MainWndProc hwnd, msgCode, wparam, lparam
	_MainWndProc:

		locals
			r	RECT
		endl

		cmp [msgCode], WM_NCHITTEST
		je .wmNcHitTest
		cmp [msgCode], WM_PAINT
		je .wmPaint
		cmp [msgCode], WM_CREATE
		je .wmCreate
		cmp [msgCode], WM_DESTROY
		je .wmDestroy
		
		.defWndProc:
			invoke DefWindowProc, [hwnd], [msgCode], [wparam], [lparam]
			jmp .wndProcEnd

		.wmNcHitTest:
			invoke GetWindowRect, [hwnd], addr r

			;instruction interleaved
			mov eax, [lparam]
			mov ecx, [lparam]
			and eax, 0000FFFFh
			shr ecx, 16

			sub [r.right], 1
			sub [r.bottom], 1
			sub [r.right], eax
			sub [r.bottom], ecx
			sub eax, [r.left]
			sub ecx, [r.top]
			mov [r.left], eax
			mov [r.top], ecx

			;tricky if condition. For details, check the value of HTTOP, HTBOTTOM, etc.
			mov eax, 9
			xor ecx, ecx
			
			cmp [r.right], 5
			jge @f
				add eax, 2 	; [r.right] < borderWidth
			jmp .ifend1
			@@:
			cmp [r.left], 5
			jge @f
				inc eax		; [r.left] < borderWidth
			jmp .ifend1
			@@:
				inc ecx		; otherwise
			.ifend1:

			cmp [r.bottom], 5
			jge @f
				add eax, 6	; [r.bottom] < borderWidth
			jmp .ifend2
			@@:
			cmp [r.top], 5
			jge @f
				add eax, 3	; [r.top] < borderWidth
			jmp .ifend2
			@@:
				inc ecx		; otherwise
			.ifend2:

			;fix the base case
			cmp ecx, 2
			cmove eax, ecx ; 2 = HTCAPTION

			jmp .wndProcEnd

		.wmPaint:
			push ebx esi edi	;for storing dc, w, h

			invoke GetWindowRect, [hwnd], addr r
			mov esi, [r.right]
			mov edi, [r.bottom]
			sub esi, [r.left]
			sub edi, [r.top]

			invoke BeginPaint, [hwnd], ps
			mov ebx, eax

			invoke SelectObject, ebx, [bgPen]
			invoke SelectObject, ebx, [bgBrush]

			invoke FillRect, ebx, ps.rcPaint, COLOR_WINDOW + 1

			invoke Rectangle, ebx, 0, 0, esi, edi

			invoke EndPaint, [hwnd], ps

			pop edi esi ebx

			xor eax, eax
			jmp .wndProcEnd

		.wmCreate:
			invoke CreateWindowEx, 0, toolWcName, wTitle, WS_CHILD + WS_VISIBLE, 10, 10, 100, 40, [hwnd], NULL, [toolWc.hInstance], NULL
			jmp .defWndProc

		.wmDestroy:
			invoke PostQuitMessage, 0
			xor eax, eax

		.wndProcEnd:
			ret
	endp

	proc ToolWndProc hwnd, msgCode, wparam, lparam
	_ToolWndProc:
		cmp [msgCode], WM_CREATE
		je .wmCreate
		cmp [msgCode], WM_COMMAND
		je .wmCommand

		.defWndProc:
			invoke DefWindowProc, [hwnd], [msgCode], [wparam], [lparam]
			jmp .wndProcEnd

		.wmCreate:
			invoke CreateWindowEx, 0, buttonWcName, btnC, WS_CHILD + WS_VISIBLE, 10, 10, 20, 20, [hwnd], 1001, [toolWc.hInstance], NULL
			invoke CreateWindowEx, 0, buttonWcName, btnS, WS_CHILD + WS_VISIBLE, 30, 10, 20, 20, [hwnd], 1002, [toolWc.hInstance], NULL
			invoke CreateWindowEx, 0, buttonWcName, btnI, WS_CHILD + WS_VISIBLE, 50, 10, 20, 20, [hwnd], 1003, [toolWc.hInstance], NULL
			invoke CreateWindowEx, 0, buttonWcName, btnX, WS_CHILD + WS_VISIBLE, 70, 10, 20, 20, [hwnd], 1004, [toolWc.hInstance], NULL
			jmp .defWndProc

		.wmCommand:
			mov eax, [wparam]
			;mov ecx, [wparam]
			and eax, 0000FFFFh
			;shr ecx, 16

			sub eax, 1001
			cmp eax, 4	; check out of bound
			jnb .defWndProc
			
			lea eax, [eax*4 + @f]
			jmp DWORD[eax]

			@@: ;jump table
			dd .btnC, .btnS, .btnI, .btnX

			.btnC:
				stdcall Capture, [mainWin], 0
				jmp @f
			.btnS:
				stdcall Capture, [mainWin], 1
				jmp @f
			.btnI:
				invoke MessageBox, [mainWin], infoMsg, infoMsgCap, MB_ICONINFORMATION + MB_OK
				jmp @f
			.btnX:
				invoke SendMessage, [mainWin], WM_CLOSE, NULL, NULL
				jmp @f

			@@:
				xor eax, eax

		.wndProcEnd:
			ret
	endp

	proc Capture, hwnd:DWORD, mode:DWORD
	_Capture:
		locals
			r	RECT
			srcDc	dd	?
			destDc	dd	?
			gdipBmp	dd	?
		endl

		push ebx esi edi	;for storing bitmap, w, h

		invoke GetWindowRect, [hwnd], addr r
		mov esi, [r.right]
		mov edi, [r.bottom]
		sub esi, [r.left]
		sub edi, [r.top]

		invoke GetDC, HWND_DESKTOP
		mov [srcDc], eax

		invoke CreateCompatibleDC, eax
		mov [destDc], eax

		invoke CreateCompatibleBitmap, [srcDc], esi, edi
		mov ebx, eax

		invoke SelectObject, [destDc], eax

		invoke ShowWindow, [mainWin], SW_MINIMIZE

		invoke BitBlt, [destDc], 0, 0, esi, edi, [srcDc], [r.left], [r.top], SRCCOPY

		invoke ShowWindow, [mainWin], SW_RESTORE

		invoke ReleaseDC, HWND_DESKTOP, [srcDc]

		invoke DeleteDC, [destDc]

		mov eax, [mode]
		or eax, eax
		jnz @f
			; to clipboard
			invoke OpenClipboard, [mainWin]

			invoke EmptyClipboard

			invoke SetClipboardData, CF_BITMAP, ebx	; ownership of the HBITMAP transferred, don't call DeleteObject on ebx

			invoke CloseClipboard

			jmp .ifend
		@@:
			; to png
			invoke GetDateFormat, 0x007f, 0, NULL, dateFormat, pngFileName.datePtr, dateFormat.wordLength
			invoke GetTimeFormat, 0x007f, 0, NULL, timeFormat, pngFileName.timePtr, timeFormat.wordLength
			mov [pngFileName.datePtrEnd], '-'	;undo the string terminations
			mov [pngFileName.timePtrEnd], '.'

			invoke ExpandEnvironmentStrings, outPath, resolvedPath, resolvedPath.wordLength

			invoke GdipCreateBitmapFromHBITMAP, ebx, NULL, addr gdipBmp

			invoke GdipSaveImageToFile, [gdipBmp], resolvedPath, pngEncoderId, NULL

			invoke GdipDisposeImage, [gdipBmp]

			invoke DeleteObject, ebx

		.ifend:

		pop edi esi ebx

		xor eax, eax
		ret
	endp


section '.idata' import data readable
	library kernel32, 	'KERNEL32.DLL', \
		user32, 	'USER32.DLL', \
		gdi32,		'GDI32.DLL', \
		gdiplus,	'GDIPLUS.DLL'
	
	include 'api/kernel32.inc'
	include 'api/user32.inc'
	include 'api/gdi32.inc'

	import  gdiplus, \
		GdiplusShutdown,'GdiplusShutdown', \
		GdiplusStartup,'GdiplusStartup', \
		GdipCreateBitmapFromHBITMAP,'GdipCreateBitmapFromHBITMAP', \
		GdipDisposeImage,'GdipDisposeImage', \
		GdipGetImageEncodersSize,'GdipGetImageEncodersSize', \
		GdipGetImageEncoders,'GdipGetImageEncoders', \
		GdipSaveImageToFile,'GdipSaveImageToFile'
